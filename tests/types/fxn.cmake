include(cmake_test/cmake_test)

ct_add_test(NAME [[test_cpp_is_fxn]])
function("${CMAKETEST_TEST}")
    include(cmakepp_lang/types/fxn)

    ct_add_section(NAME [[test_signature]] EXPECTFAIL)
    function("${CMAKETEST_SECTION}")
        cpp_is_fxn(return add_subdirectory hello)
    endfunction()

    ct_add_section(NAME [[test_bool]])
    function("${CMAKETEST_SECTION}")
        cpp_is_fxn(return TRUE)
        ct_assert_equal(return FALSE)
    endfunction()

    ct_add_section(NAME [[test_class]])
    function("${CMAKETEST_SECTION}")
        include(cmakepp_lang/class/class)
        cpp_class(MyClass)

        cpp_is_fxn(return MyClass)
        ct_assert_equal(return FALSE)
    endfunction()

    ct_add_section(NAME [[test_command]])
    function("${CMAKETEST_SECTION}")
        cpp_is_fxn(return add_subdirectory)
        ct_assert_equal(return TRUE)
    endfunction()

    ct_add_section(NAME [[test_descriptions]])
    function("${CMAKETEST_SECTION}")
        ct_add_section(NAME [[description_without_fxn]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return "Hello World")
            ct_assert_equal(return FALSE)
        endfunction()

        ct_add_section(NAME [[description_contains_fxn]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return "Hello World add_subdirectory")
            ct_assert_equal(return FALSE)
        endfunction()
    endfunction()

    ct_add_section(NAME [[test_float]])
    function("${CMAKETEST_SECTION}")
        cpp_is_fxn(return 3.14)
        ct_assert_equal(return FALSE)
    endfunction()

    ct_add_section(NAME [[test_integer]])
    function("${CMAKETEST_SECTION}")
        cpp_is_fxn(return 42)
        ct_assert_equal(return FALSE)
    endfunction()

    ct_add_section(NAME [[test_list]])
    function("${CMAKETEST_SECTION}")
        ct_add_section(NAME [[normal_list]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return [[hello;world]])
            ct_assert_equal(return FALSE)
        endfunction()

        ct_add_section(NAME [[list_of_fxns]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return [[add_subdirectory;include]])
            ct_assert_equal(return FALSE)
        endfunction()
    endfunction()

    ct_add_section(NAME [[test_map]])
    function("${CMAKETEST_SECTION}")
        include(cmakepp_lang/map/map)
        cpp_map(CTOR a_map)
        cpp_is_fxn(result a_map)
        ct_assert_equal(result FALSE)
    endfunction()

    ct_add_section(NAME [[test_obj]])
    function("${CMAKETEST_SECTION}")
        include(cmakepp_lang/object/object)
        cpp_is_fxn(result "${__CMAKEPP_LANG_OBJECT_SINGLETON__}")
        ct_assert_equal(result FALSE)
    endfunction()

    ct_add_section(NAME [[test_path]])
    function("${CMAKETEST_SECTION}")
        ct_add_section(NAME [[normal_path]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return "${CMAKE_CURRENT_LIST_DIR}")
            ct_assert_equal(return FALSE)
        endfunction()

        ct_add_section(NAME [[path_contains_fxn]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return "${CMAKE_CURRENT_LIST_DIR}/add_subdirectory")
            ct_assert_equal(return FALSE)
        endfunction()
    endfunction()

    ct_add_section(NAME [[test_target]])
    function("${CMAKETEST_SECTION}")
        ct_add_section(NAME [[normal_target_name]])
        function("${CMAKETEST_SECTION}")
            add_library(lib STATIC IMPORTED)
            cpp_is_fxn(return lib)
            ct_assert_equal(return FALSE)
        endfunction()

        ct_add_section(NAME [[target_with_fxn]])
        function("${CMAKETEST_SECTION}")
            add_library(libadd_subdirectory STATIC IMPORTED)
            cpp_is_fxn(return lib1)
            ct_assert_equal(return FALSE)
        endfunction()
    endfunction()

    ct_add_section(NAME [[test_type]])
    function("${CMAKETEST_SECTION}")

        ct_add_section(NAME [[type_not_command]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return bool)
            ct_assert_equal(return FALSE)
        endfunction()

        ct_add_section(NAME [[list_which_is_also_command]])
        function("${CMAKETEST_SECTION}")
            cpp_is_fxn(return list)
            ct_assert_equal(return FALSE)
        endfunction()
    endfunction()
endfunction()
